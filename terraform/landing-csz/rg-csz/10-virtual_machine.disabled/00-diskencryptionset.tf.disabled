resource "azurerm_key_vault_key" "disk-encryption-key" {
  name         = "disk-encryption-key"
  key_vault_id = data.terraform_remote_state.global_rg.outputs.keyvault_id
  key_type     = "RSA"
  key_size     = 2048

  key_opts = [
    "decrypt",
    "encrypt",
    "sign",
    "unwrapKey",
    "verify",
    "wrapKey",
  ]
}

###  To use this element, purge_protection needs to be set to True in the KeyVault
resource "azurerm_disk_encryption_set" "disk-encryption-set" {
  name                  = "disk-encryption-set"
  location              = data.terraform_remote_state.global_rg.outputs.resource_group_location
  resource_group_name   = data.terraform_remote_state.global_rg.outputs.resource_group_name
  key_vault_key_id      = azurerm_key_vault_key.disk-encryption-key.id

  identity {
    type = "SystemAssigned"
  }
}

resource "azurerm_key_vault_access_policy" "disk-encryption-set-policy" {
  key_vault_id     = data.terraform_remote_state.global_rg.outputs.keyvault_id

  tenant_id        = azurerm_disk_encryption_set.disk-encryption-set.identity.0.tenant_id
  object_id        = azurerm_disk_encryption_set.disk-encryption-set.identity.0.principal_id

  key_permissions  = [
    "Get",
    "WrapKey",
    "UnwrapKey"
  ]
}
